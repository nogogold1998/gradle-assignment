plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion rootProject.ext.compileSdk_version
    buildToolsVersion rootProject.ext.buildTools_version

    defaultConfig {
        applicationId "com.example.assignment.gradle"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "task", "multiThreading"
    productFlavors {
        asyncTask {
            dimension "task"
            applicationIdSuffix ".asyncTask"
        }
        handlerThread {
            dimension "task"
            applicationIdSuffix ".handlerThread"
        }
        coroutines {
            dimension "task"
            applicationIdSuffix ".coroutines"
        }
        callback {
            dimension "multiThreading"
        }
        flow {
            dimension "multiThreading"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (!(names.contains("coroutines") ^ names.contains("callback"))) {
            setIgnore true
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$androidx_core_ktx_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    // https://developer.android.com/training/permissions/requesting#allow-system-manage-request-code
    implementation "androidx.activity:activity-ktx:$activity_version"
    // dependent package
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    // ViewModel + LiveData ktx
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
//    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
//    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    testImplementation "junit:junit:$junit_version"

    androidTestImplementation "androidx.test.ext:junit:$androidx_ext_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"


}
